# shellcheck shell=bash disable=SC2034 disable=SC2154
# Maintainer: Lucas Melo <luluco250 at gmail dot com>

pkgname=soniccd-git
pkgver=r712.6ecf959
pkgrel=1
pkgdesc='A full decompilation of Sonic CD 2011, based on the PC remake with
improvements & tweaks from the mobile remakes.'
arch=('powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url='https://github.com/RSDKModding/RSDKv3-Decompilation'
license=('custom:RSDKv3/4 Decompilation Source Code License v1')
makedepends=('git' 'pkg-config')
depends=('cmake' 'glew' 'sdl2' 'libogg' 'libtheora' 'libvorbis')
options=('!debug' 'strip')
provides=(soniccd)
source=(
	"git+${url}.git"
	'soniccd-launcher'
	'soniccd.desktop'
	'big-endian-fix.patch'
)
sha256sums=(
	'SKIP'
	'b70b6526c137859385234a19d1935e089c3c6a71639f42f7d579135579e8243b'
	'8f24c0150f426a693271317a017ffc11116e6710bbc7a7589542a913bd78adac'
	'518f48d89254d105ff03d6f6c164792e6e9355081b5387373e32793856d8746b'
)
install=soniccd.install

_safe_cd() {
	if ! cd "$1"; then
		>&2 echo "Directory '$1' not found"
		exit 1
	fi
}

pkgver() {
	_safe_cd "$srcdir/RSDKv3-Decompilation"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	_safe_cd "$srcdir/RSDKv3-Decompilation"
	git submodule init
	git -c protocol.file.allow=always submodule update
	patch -Np1 -i "${srcdir}/big-endian-fix.patch"
}

build() {
	_safe_cd "$srcdir/RSDKv3-Decompilation"
	cmake -B build
	cmake --build build --config release
}

package() {
	install -Dm755 soniccd-launcher "$pkgdir/usr/bin/soniccd-launcher"
	install -Dm644 soniccd.desktop "$pkgdir/usr/share/applications/soniccd.desktop"
	install -dm755 "$pkgdir/usr/share/icons"
	cp -r "$srcdir/RSDKv3-Decompilation/flatpak/com.sega.SonicCD.svg" "$pkgdir/usr/share/icons"
	_safe_cd "$srcdir/RSDKv3-Decompilation"
	install -Dm755 ./build/RSDKv3 "$pkgdir/usr/bin/RSDKv3"
	install -Dm644 ./LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
