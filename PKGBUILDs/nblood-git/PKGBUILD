# Maintainer: Sid <sidpranjale127@protonmail.com>
# Continuing the work of:
# Maintainer: Jan Cholasta <jan at cholasta net>

pkgname=nblood-git
pkgver=r14326.6351794a0
pkgrel=1
pkgdesc='Blood port based on EDuke32 (git version)'
arch=('powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url='http://nukeykt.retrohost.net/'
license=('GPL' 'custom:BUILD')
depends=('flac'
         'libogg'
         'libvorbis'
         'libvpx'
         'sdl2'
)
makedepends=('git' 'glu' 'imagemagick')
optdepends=('blood-demo')
provides=('nblood')
conflicts=('nblood')
options=('!debug' 'strip')
source=('git+https://github.com/NBlood/NBlood.git'
        'nblood.png'
        'nblood.desktop'
        'big-endian-fix.patch')
sha256sums=('SKIP'
            '8061b8ca92b0acab82c2c79e5da71b8d0acb82502c7c96e9ef86b09cfcd83da5'
            '58807697e95e071a6354f87e8e2fdae9f32af0e08dad1a510a0089845df42184'
            'bc028f6bafaf255ab412749072cdee9b60dc3c37338910dc7d882f9a1b96710f')

pkgver() {
    cd $srcdir/NBlood
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
    patch -Np1 -i "$srcdir"/big-endian-fix.patch -d "$srcdir/NBlood"
    cd NBlood
    make blood
}

package() {
    cd NBlood
    install -D -t "$pkgdir"/usr/bin nblood
    install -D -t "$pkgdir"/usr/share/games/nblood -m 644 nblood.pk3
    install -D -t "$pkgdir"/usr/share/licenses/$pkgname -m 644 package/common/buildlic.txt
    install -Dm644 "$srcdir"/nblood.png "$pkgdir"/usr/share/pixmaps/nblood.png
    install -D -t "$pkgdir"/usr/share/applications -m 644 "$srcdir"/nblood.desktop
    echo "Place your game files in ~/.config/nblood"
}
