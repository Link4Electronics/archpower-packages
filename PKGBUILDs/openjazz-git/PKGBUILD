# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=openjazz-git
_pkgname=${pkgname%-*}
pkgver=20240919.r39.g03c2503
pkgrel=1
pkgdesc="A free, open-source version of the classic Jazz Jackrabbitâ„¢ games (development version)"
arch=('powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url="http://alister.eu/jazz/oj/"
license=('GPL')
conflicts=("$_pkgname")
provides=("$_pkgname")
makedepends=('git' 'cmake' 'ninja')
depends=('sdl2')
options=('!debug' 'strip')
install=$_pkgname.install
source=($_pkgname::"git+https://github.com/AlisterT/openjazz.git")
sha256sums=('SKIP')

pkgver() {
  git -C $_pkgname describe --long | sed 's/-/.r/;s/-/./'
}

prepare() {
  rm -rf aurbuild
}

build() {
  cmake -S $_pkgname -B aurbuild -G Ninja \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATAPATH="/usr/share/openjazz/"
  cmake --build aurbuild
}

package() {
  DESTDIR="$pkgdir/" cmake --install aurbuild
}
