# Maintainer: AltoXorg <atrl101 AT yahoo DOT com>

_reponame=2ship2harkinian
_lus_commit=74c78f771f20af9c90071aaefa7d2b49075f2f68
_ZAPDTR_commit=b002f28757a7ff9f928a4d9dd1834137edb144bb
_OTRExporter_commit=375489d5f1f5fa4b9144ac9669c411e3b149f32a

pkgbase=2s2h
pkgname=2s2h
pkgver=3.0.0
pkgrel=1
arch=('powerpc' 'powerpc64' 'powerpc64le' 'espresso')
#url="https://shipofharkinian.com/"
url="https://github.com/HarbourMasters/2ship2harkinian-WiiU"
_depends_2s2h=("sdl2" "zenity" "libpng" "libogg" "libvorbis" "opus" "opusfile")
_depends_2s2h_otr_exporter=("libpng")
_depends_lus=("libzip" "fmt" "spdlog" "tinyxml2")
depends=("${_depends_2s2h[@]}" "${_depends_2s2h_otr_exporter[@]}" "${_depends_lus[@]}")
makedepends=("git" "cmake" "ninja" "python" "curl" "lsb-release" "boost" "nlohmann-json")
options=('!debug' 'strip')
source=("git+https://github.com/HarbourMasters/2ship2harkinian-WiiU.git"
        "libultraship-wiiu-${_lus_commit:0:8}.tar.gz::https://github.com/HarbourMasters/libultraship-wiiu/archive/${_lus_commit}.tar.gz"
        "OTRExporter-${_OTRExporter_commit:0:8}.tar.gz::https://github.com/louist103/OTRExporter/archive/${_OTRExporter_commit}.tar.gz"
        "ZAPDTR-${_ZAPDTR_commit:0:8}.tar.gz::https://github.com/louist103/ZAPDTR/archive/${_ZAPDTR_commit}.tar.gz"
        "2s2h.desktop"
        "imgui-befix.patch")
sha256sums=('SKIP'
            '55d763f294bdeac37cdc9c2663487be79b96d45626190113ad218948aeccd110'
            'd6d5b6b580d1e76e1066df368784bd94ce711686cd74cfcb765e518ba51b4f63'
            'ce6c7de6ca1837770611b95ac81103d01e070916aca00afd358499a24da72365'
            '8c8525a09a94c30106f44219c0f0239789d5c53cd6e30c7748b55f8d83cee24b'
            'ad180e0adfa19935acdc11d2ee5cd4668684d92edc6f8f7451d3404e478d638d')

# -- Per-repo submodules
_main_submodules=(
  libultraship
  OTRExporter
  ZAPDTR
)

SHIP_PREFIX=/opt/2s2h

_is_debug() {
  for opt in "${OPTIONS[@]}"; do
    if [ "$opt" = debug ]; then
      return 0
    fi
  done

  return 1
}


prepare() {
  cd "${srcdir}/${_reponame}-WiiU"

  rm -r libultraship ZAPDTR OTRExporter
  cp -r ../libultraship-wiiu-${_lus_commit} libultraship
  #cp -r ../libultraship-${_lus_tag} libultraship
  cp -r ../ZAPDTR-${_ZAPDTR_commit} ZAPDTR
  cp -r ../OTRExporter-${_OTRExporter_commit} OTRExporter
}

build() {
  cd "${srcdir}/${_reponame}-WiiU"

  if _is_debug; then
    BUILD_TYPE=Debug
  else
    BUILD_TYPE=Release
  fi

  export CFLAGS="${CFLAGS/-Werror=format-security/}"
  export CXXFLAGS="${CXXFLAGS/-Werror=format-security/}"

  # Compile error fix, might be committed to upstream
  CFLAGS+=" -Wno-return-mismatch"

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=$BUILD_TYPE \
    -DNON_PORTABLE=On \
    -DCMAKE_INSTALL_PREFIX=$SHIP_PREFIX
    #-DBUILD_REMOTE_CONTROL=1

  patch -Np1 -i "${srcdir}/imgui-befix.patch" -d "build/_deps/imgui-src/"
  cmake --build build --target ZAPD --config $BUILD_TYPE $NINJAFLAGS
  cmake --build build --target Generate2ShipOtr --config $BUILD_TYPE $NINJAFLAGS
  cmake --build build --config $BUILD_TYPE $NINJAFLAGS
}

package_2s2h() {
  pkgdesc="An unofficial port of The Legend of Zelda Majora's Mask"
  depends=("${_depends_2s2h[@]}" "${_depends_lus[@]}")
  license=("CC0-1.0")
  install=2s2h.install

  cd "${srcdir}/${_reponame}-WiiU"

  DESTDIR="${pkgdir}" cmake --install build --component ship
  DESTDIR="${pkgdir}" cmake --install build --component 2s2h

  install -dm755 "${pkgdir}/usr/bin/"
  ln -s "${SHIP_PREFIX}/2s2h.elf" "${pkgdir}/usr/bin/2s2h"
  install -Dm644 "${srcdir}/2s2h.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 mm/linux/2s2hIcon.png "${pkgdir}/usr/share/pixmaps/2s2h.png"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/2s2h/LICENSE"

  # Copy game documentation
  install -Dm644 "README.md" "${pkgdir}/usr/share/doc/2s2h/README.md"
  #cp -r docs "${pkgdir}/usr/share/doc/2s2h/docs"
}
